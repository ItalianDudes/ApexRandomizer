plugins {
    id 'com.gradleup.shadow' version '8.3.6'
    id 'java'
}

// Versioning
def versionNumber = '1.1.0'
def versionType = 'RELEASE' // RELEASE | BETA | ALPHA | DEV
def versionMarker = versionType.charAt(0)

group = 'it.italiandudes'
version = versionNumber+versionMarker

repositories {
    mavenCentral()
    flatDir {
        dirs 'lib'
    }
}

// Configuration for annotation as Lombok
configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

dependencies {
    compileOnly 'org.jetbrains:annotations:26.0.1'
    implementation 'org.apache.commons:commons-lang3:3.18.0'
    implementation 'commons-io:commons-io:2.18.0'
    implementation 'commons-codec:commons-codec:1.17.1'
    implementation 'it.italiandudes:idl:1.2.2'
    implementation 'org.json:json:20240303'
    compileOnly 'org.projectlombok:lombok:1.18.42'
    annotationProcessor 'org.projectlombok:lombok:1.18.42'
}

shadowJar {
    manifest {
        attributes "Main-Class" : 'it.italiandudes.apexrandomizer.ApexRandomizer'
        attributes "Title" : "Apex Randomizer"
        attributes "Vendor" : "ItalianDudes"
        attributes "Version" : archiveVersion.get()
        attributes "Version-Number" : archiveVersion.get()
        attributes "Version-Type" : versionType
        attributes "Version-Marker" : versionMarker
        attributes "Timestamp" : new Date().format("dd/MM/yyyy HH:mm:ss.sss")
        attributes "ID-Launcher-Enabled" : false
    }
    archiveFileName = getRootDir().getName() + "-" + archiveVersion.get() + ".jar"
}

tasks.register('buildJar') {
    dependsOn shadowJar
    group = "Build"
    description = "Build and assemble ApexRandomizer"
}

tasks.register('runClient', Exec) {
    dependsOn buildJar
    group = "Execution"
    description = "Build, assemble and run ApexRandomizer"
    standardInput = System.in
    commandLine "java", "-jar", jar.archiveFile.get()
}